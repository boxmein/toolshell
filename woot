#!/bin/zsh
# Purpose: DSL for project tooling
# Usage: source this file, and start writing commands
set -e

#
# Configuration
#
# - TOOL_NAME: name of the build tool
# - PROJECT_NAME: name of the project


#
#
# State
#
#

declare -a _woot_cleanup_folders
declare -a _woot_setup_checks
declare -A _woot_task_map
_woot_build_command=""
_woot_start_command=""

#
#
# Tool checkers
#
#

_woot_check_tool_in_path() {
  tool_name="$1"
  tool_expected_version="$2"

  if ! command -v "$tool_name" >/dev/null 2>/dev/null; then 
    echo "Tool not found: $tool_name"
    exit 1
  fi

  tool_version=$(${tool_name} --version)
  if [[ "$tool_version" != *${tool_expected_version}* ]]; then 
    echo "Tool is the wrong version: $tool_name $tool_version"
    echo "Expected $tool_expected_version"
  fi
}

#
#
# State setters
#
#

_woot_set_build_command() {
  _woot_build_command="$1"
}

_woot_set_start_command() {
  _woot_start_command="$1"
}

_woot_create_task_command() {
  _woot_task_map[$1]="$2"
}

_woot_create_setup_check() {
  _woot_setup_checks+=("$*")
}

_woot_declare_cleanup_folders() {
  _woot_cleanup_folders=($*)
}

#
#
# Runners
#
#

_woot_invoke_task() {
  sh -c "${_woot_task_map[$*]}"
}

_woot_invoke_build_command() {
  sh -c "${_woot_build_command}"
}

_woot_invoke_start_command() {
  sh -c "${_woot_start_command}"
}

_woot_invoke_cleanup() {
  for folder in $_woot_cleanup_folders; do
    rm -rf $folder;
  done
}

_woot_invoke_setup_checks() {
  for check in $_woot_setup_checks; do 
    sh -c "${check}" || echo "Error: $check failed." && exit 1
  done
}

#
#
# CLI
#
#

_woot_usage() {
  local toolName=${TOOL_NAME:-buildtool}
  local projectName=${PROJECT_NAME:-this project}
  cat <<EOF
$toolName - tooling for $projectName

usage: 

  $toolName [args]

  where:

            -v:   enable verbose mode for the build tool
    -h, --help:   get this help text
EOF

  if [[ -n "$_woot_build_command" ]]; then 
    echo "         build:   build $projectName"
  fi
  if [[ -n "$_woot_start_command" ]]; then 
    echo "         start:   start the dev mode for $projectName"
  fi
  if [[ -n "${_woot_clean_folders}" ]]; then
    echo  "         clean:   remove output folders"
  fi
  if [[ -n "${(k)_woot_task_map}" ]]; then 
    echo "          run:   start a script for $projectName"
    echo
    echo "available scripts: "
    for task in ${(k)_woot_task_map}; do
      echo "$toolName $task"
    done
  fi
}

_woot_fuzzy_ui() {
  task_to_run=$(echo ${(j:.:)${(k)_woot_task_map}} | tr '.' '\n' | fzf)
  echo "Running ./x $task_to_run"
  _woot_invoke_task "$task_to_run"
}

_woot_fallback() {
  if [[ -n "${_woot_task_map[$*]}" ]]; then 
    _woot_invoke_task "$*"
  else
    _woot_fuzzy_ui "$*"
  fi
}


_woot_parse_cli_and_invoke() {

  _woot_invoke_setup_checks

  local didSomething=0
  while [[ $# -gt 0 ]]; do
    case $1 in
      -v)
        set -x
        ;;
      -h|--help)
        _woot_usage
        didSomething=1
        ;;
      build)
        _woot_invoke_build_command
        didSomething=1
        ;;
      start)
        _woot_invoke_start_command
        didSomething=1
        ;;
      run)
        shift
        _woot_invoke_task "$*"
        didSomething=1
        ;;
      clean|cleanup)
        _woot_invoke_cleanup
        didSomething=1
        ;;
      *)
        _woot_fallback "$*"
        didSomething=1
        ;;
    esac
    shift
  done

  if [[ "$didSomething" -ne 1 ]]; then 
    _woot_fallback "$*"
  fi
}

alias build="_woot_set_build_command"
alias run="_woot_create_task_command"
alias cleanup="_woot_declare_cleanup_folders"
alias woot='_woot_parse_cli_and_invoke $*'
alias tool='_woot_check_tool_in_path'
alias start='_woot_set_start_command'
alias check='_woot_create_setup_check'
